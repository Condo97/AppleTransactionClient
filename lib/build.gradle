plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'io.github.condo97'
version = System.getenv('VERSION') ?: '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri("https://maven.pkg.github.com/Condo97/HttpsHandler")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME") ?: System.getenv("GITHUB_ACTOR") ?: ""
            password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN") ?: ""
        }
    }
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:31.1-jre'

    // Jackson for JSONs
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.0.1'

    // Httpson for HTTPS handling with Jackson and JsonNode
    implementation 'io.github.condo97:httpson:1.0.0'

    // java-jwt for JWT
    implementation 'com.auth0:java-jwt:4.4.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        gpr(MavenPublication) {
            from components.java
            artifactId = 'apple-transaction-client'
            
            pom {
                name = 'Apple Transaction Client'
                description = 'Java client library for Apple App Store Server API transactions and subscription management'
                url = 'https://github.com/Condo97/AppleTransactionClient'
                
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                
                developers {
                    developer {
                        id = 'condo97'
                        name = 'Condo97'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/Condo97/AppleTransactionClient.git'
                    developerConnection = 'scm:git:ssh://github.com/Condo97/AppleTransactionClient.git'
                    url = 'https://github.com/Condo97/AppleTransactionClient'
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Condo97/AppleTransactionClient")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") ?: ""
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token") ?: ""
            }
        }
    }
}
